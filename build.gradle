plugins {
    id 'fabric-loom' version '0.2.7-SNAPSHOT'
    id 'maven-publish'
    id "co.riiid.gradle" version "0.4.2"
    id "com.matthewprenger.cursegradle" version "1.4.0"
    id "com.wynprice.cursemaven" version "2.1.0"
}

repositories {
    maven{ url="https://minecraft.curseforge.com/api/maven" }
    maven{ url="https://maven.jamieswhiteshirt.com/libs-release/" }
    maven{ url="https://maven.fabricmc.net" }
    maven { url = "https://maven.swordglowsblue.com" }
    maven { url = 'http://maven.sargunv.s3-website-us-west-2.amazonaws.com/' }
    maven {
        name = "Cotton"
        url = "http://server.bbkr.space:8081/artifactory/libs-release"
    }
	maven { url "https://dl.bintray.com/shedaniel/autoconfig1u/" }
    maven {
        name = "NerdHubMC"
        url = "https://maven.abusedmaster.xyz/"
    }

	maven { url = "https://jitpack.io"}
    jcenter()
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
def buildnumber = ""
def gittoken = System.getenv('GH_TOKEN')
if (System.getenv('TRAVIS_BUILD_NUMBER') == "null" || System.getenv('TRAVIS_BUILD_NUMBER') == null) {
    buildnumber = project.beta_version
} else {
    buildnumber = System.getenv('TRAVIS_BUILD_NUMBER')
}
def buildbranch = System.getenv('TRAVIS_BRANCH')

archivesBaseName = project.archives_base_name
version = project.mod_version + "+build." + buildnumber
group = project.maven_group

minecraft {
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

    modCompile "net.fabricmc:fabric-loader:${project.loader_version}"

    modCompile "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modImplementation "com.lettuce.fudge:artifice:0.6.0+20w06a"
    include ("com.lettuce.fudge:artifice:0.6.0+20w06a")

    modImplementation "com.github.vampire-studios:VampireLib-Fabric:${project.vampirelib_version}"
    include "com.github.vampire-studios:VampireLib-Fabric:${project.vampirelib_version}"

//    modRuntime 'curse.maven:slightly-altered-terrain-generation:2834529'
//    modRuntime 'curse.maven:trees-do-not-float:2833870'
//    modRuntime 'curse.maven:carpet:2834141'
//    modRuntime 'curse.maven:fabric-vertigo:2834529'
//    modRuntime 'curse.maven:expanded-storage:2832473'
//    modRuntime 'curse.maven:carpet-extra:2830934'
//    modRuntime 'curse.maven:shulkerboxtooltip:2829477'
//    modRuntime 'curse.maven:leaf-decay:2827905'
    modImplementation("com.github.Draylar:world-traveler:${world_traveler_version}") {
        transitive = false
    }

    modImplementation "curse.maven:immersive-portals-mod:2893204"

    modImplementation 'com.github.SuperCoder7979:simplexterrain:feature~terrain-rewrite-SNAPSHOT'

    modCompile "me.sargunvohra.mcmods:autoconfig1u:${project.auto_config_version}"
    include ("me.sargunvohra.mcmods:autoconfig1u:${project.auto_config_version}") {
        exclude group :"net.fabricmc.fabric-api"
    }

    modCompile "me.shedaniel.cloth:config-2:${project.cloth_config_version}"
    include ("me.shedaniel.cloth:config-2:${project.cloth_config_version}") {
        exclude group :"net.fabricmc.fabric-api"
    }

    modImplementation ("com.github.CottonMC:Jankson-Fabric:-SNAPSHOT")

    include ("me.zeroeightsix:fiber:0.6.0-7") {
        exclude group :"net.fabricmc.fabric-api"
    }

    modImplementation("me.shedaniel:RoughlyEnoughItems:${project.rei_version}") {
        exclude group: "io.github.prospector.modmenu"
        exclude group :"net.fabricmc.fabric-api"
    }
}

github {
    owner = 'vampire-studios'
    repo = 'Randomly-Adding-Anything'
    token = gittoken ?: "a key"
    tagName = version + "-" + project.minecraft_version.replace(" ", "-")
    name = version + "-" + "MC" + project.minecraft_version.replace(" ", "-")
    targetCommitish = buildbranch
    prerelease = true
    assets = [
            "build/libs/${archivesBaseName}-${version}-dev.jar",
            "build/libs/${archivesBaseName}-${version}.jar",
            "build/libs/${archivesBaseName}-${version}-sources-dev.jar",
            "build/libs/${archivesBaseName}-${version}-sources.jar"
    ]
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

jar {
    from "LICENSE"
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            // add all the jars that should be included when publishing to maven
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    // select the repositories you want to publish to
    repositories {
        // uncomment to publish to the local maven
        // mavenLocal()
    }
}

curseforge {
    apiKey = System.getenv('CURSEKEY') ?: "a key"
    project {
        id = "341464"
        releaseType = "release"
        changelog = project.changelog
        gameVersionStrings.clear()
        gameVersionStrings.add("1.15-Snapshot")
        gameVersionStrings.add("1.15")
        gameVersionStrings.add("1.15.1")
        gameVersionStrings.add("Fabric")
        mainArtifact(file("build/libs/${archivesBaseName}-${version}.jar")) {
            displayName = "[" + minecraft_version + "] " + version
            relations {
                requiredDependency "fabric-api"
                embeddedLibrary "auto-config"
            }
        }
    }
    options{
        forgeGradleIntegration = false
    }
}
